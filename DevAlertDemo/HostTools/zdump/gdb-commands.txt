
printf "\n\nCore Dump Summary Report\n\n"

set print address off
set logging file ~/zdump-test/gdb-output.txt
set logging overwrite on
set logging enabled on
set pagination off
set confirm off
set filename-display basename
set verbose off
target remote localhost:1234

printf "\nCore Registers\n"
info registers

printf "\nCall Stack:\n"
bt

printf "\nFault Status Registers (CFSR):\n"
printf " - UFSR, UsageFault Status Register (0xE000ED2A): 0x%04X \n", *(uint16_t *)0xE000ED2A
printf " - BFSR, BusFault Status Register   (0xE000ED29): 0x%02X \n", *(uint8_t *)0xE000ED29
printf " - MMFSR, MemManage Status Register (0xE000ED28): 0x%02X \n", *(uint8_t *)0xE000ED28
printf "\nFor more information about Arm Cortex-M fault status registers, see https://developer.arm.com/documentation/dui0552/a/cortex-m3-peripherals/system-control-block/configurable-fault-status-register.\n"

printf "\n\nYou may configure the Core Dump Report by editing gdb-commands.txt.\n"


quit